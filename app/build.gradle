plugins {
    id 'com.android.application'
    id 'de.undercouch.download' version '5.6.0'
}

android {
    namespace 'com.rokid.openxr.android'
    compileSdk 34

    defaultConfig {
        applicationId "com.rokid.openxr.android"
        minSdk 29
        targetSdk 34
        versionCode 30000
        versionName "3.0.0"

        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
                arguments "-DANDROID_STL=c++_shared"
                abiFilters 'arm64-v8a'
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }

    android.applicationVariants.all { variant ->
        def versionName = variant.versionName
        def versionCode = variant.versionCode
        variant.outputs.all {
            outputFileName = "RokidOpenXRAndroidDemo-v${versionName}.apk"
        }
    }

    buildFeatures {
        viewBinding true
    }
}


tasks.register("downloadFile", de.undercouch.gradle.tasks.download.Download) {
    src "https://github.com/xiaowuga/AppVer2/releases/download/v1.0.0/third_party.zip"
    dest layout.buildDirectory.file("deps/third_party.zip")

    // Gradle 输出声明，文件存在就跳过下载
    outputs.file(layout.buildDirectory.file("deps/third_party.zip"))
    overwrite false
}

// 解压任务
tasks.register("unzipFile", Copy) {
    dependsOn("downloadFile")
    from(zipTree(layout.buildDirectory.file("deps/third_party.zip")))
    into(layout.projectDirectory.dir("src/main/cpp"))

    // 声明输出，Gradle 会判断是否需要解压
    outputs.dir(layout.projectDirectory.dir("src/main/cpp/third_party"))
}

// 把解压任务挂到构建流程里，保证 build 前自动执行
tasks.named("preBuild") {
    dependsOn("unzipFile")
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}